http                      =  $Options["HTTPUHandler"];
env                       =  $Options["Env"];
AppEvent                  =  $Options["AppEvent"];
ExtensionTemplates        =  $Options["ExtensionTemplates"];
ExtensionSidebarIcons        =  $Options["ExtensionSidebarIcons"];
middleware                = $Options["Middleware"];


Head        = ImportComponent["Components/Head.wlx"];

Kernels   =  ImportComponent["Components/Kernel/Minimal.wlx"];

Notifications  = ImportComponent["Components/Notifications/Notifications.wlx"];

Alert           = ImportComponent["Components/Alert.wlx"];

Modals           = ImportComponent["Components/Modals/Modals.wlx"];

Views            = ImportComponent["Views.wl"];

TopBar           = ImportComponent["Components/Topbar.wlx"];

ElectronBinds           = ImportComponent["Components/ElectronAppBinds.wlx"];

BrowserMenu            = ImportComponent["Components/FakeMenuBrowser.wlx"];

FakeMenu            = ImportComponent["Components/FakeMenu.wlx"];

{saveNotebook, loadNotebook, renameNotebook, cloneNotebook}         = ImportComponent["Loader.wl"];
{deinitKernel, initKernel}           = ImportComponent["KernelUtils.wl"];

{loadSettings, storeSettings}        = ImportComponent["Settings.wl"];

autolaunch                           = ImportComponent["Autolaunch.wl"];

serverParams = <|"env" -> env|>;
settings = <||>;

loadSettings[settings];
loadSettingsOnceMore := With[{},
  Print["Load settings one more time"];
  loadSettingsOnceMore = Null;
  loadSettings[settings];
];


NotebookQ[str_] := FileExtension[str] === "wln"

reopenNotebookAsQuick[_, GlobalMessanger_, __] := EventFire[GlobalMessanger, "Info", "You are already in a quick view"]


renameFile[notebook_nb`NotebookObj, name_String, client_] := Module[{filename = name, oldPath = notebook["Path"]},
  If[StringTake[filename, -3] =!= "wln", filename = StringJoin[filename, ".wln"]];
  filename = FileNameJoin[Join[Drop[FileNameSplit[notebook["Path"]], -1], {filename}]];
  RenameFile[oldPath, filename];
  renameNotebook[notebook, filename];
  WebUILocation[StringJoin["/folder/", URLEncode[notebook["Path"]]], client];
]

createNotebook[props_Association, assoc_Association, GlobalMessanger_, ModalController_, client_, currentpath_] := With[{path = assoc["BaseDirectory"]},
  Print[StringJoin["Creating notebook using path : ", path]];
  Then[saveNotebook[path, "Props"->props, "Events"->AppEvent, "Modals"->ModalController, "Client"->client], Function[result,

      Print[result];
      EventFire[AppEvent, Notebook["OnCreate"], result];
      EventFire[GlobalMessanger, "Log", "Created! Please wait"];
      Print["Redirect"];
      Print[result["Path"]];
      
      WebUILocation[ StringJoin["/", URLEncode[ result["Path"] ]], client, "Target"->_, "Features"->"left=100,top=100"];
      
    
  ], Function[error,
    EventFire[GlobalMessanger, "Error", "Failed to create a new notebook! Check if the working directory is writable"];
  ]]
]

backupNotebook[notebook_nb`NotebookObj, GlobalMessanger_, client_] := With[{},
  If[!TrueQ[notebook["ReadOnly?"]], 
    EventFire[GlobalMessanger, "Notebook:Saving:Started", True];
    Then[saveNotebook[notebook, "Events"->AppEvent, "Temporal"->True], Function[result,
        EventFire[GlobalMessanger, "Notebook:Saving:Finished", True];
        Print[result];
      ], Function[error, 
          EventFire[GlobalMessanger, "Notebook:Saving:Finished", True];
          EventFire[GlobalMessanger, "Error", "Failed to save a notebook!"]
      ]
    ]
  ]
]

dumpNotebook[notebook_nb`NotebookObj, GlobalMessanger_, ModalController_, client_] := With[{promise = Promise[]},
  If[TrueQ[notebook["ReadOnly?"]],
    EventFire[GlobalMessanger, "Error", "This is read-only notebook"];
  ,
    If[TrueQ @ (notebook["HaveToSaveAs"]),
      With[{
          p = Promise[]
      },
          EventFire[ModalController, "SaveDialog", <|
              "Promise"->p,
              "title"->"Save notebook",
              "properties"->{"createDirectory", "dontAddToRecent"},
              "filters"->{<|"extensions"->"wln", "name"->"WLJS Notebook"|>}
          |>];
          
          Then[p, Function[result, 
              Module[{filename = If[StringQ[result], URLDecode @ result, URLDecode @ result["filePath"]]},
                  If[!StringQ[filename] || TrueQ[result["canceled"] ] || StringLength[filename] === 0, 
                    Echo["Cancelled saving"]; Echo[result];
                    EventFire[GlobalMessanger, "Notebook:Saving:Finished", True]; 
                    Return[];
                  ];

                  If[!StringMatchQ[filename, __~~".wln"],  filename = StringJoin[filename, ".wln"]];
                  If[filename === ".wln", filename = StringJoin[name, filename] ];
                  If[DirectoryName[filename] === "", filename = FileNameJoin[{path, filename}] ];
                  
                  EventFire[GlobalMessanger, "Notebook:Saving:Started", True];
                  notebook["WorkingDirectory"] = .;

                  Then[saveNotebook[filename, notebook, "Events"->AppEvent, "Modals"->ModalController, "Client"->client], 
                    Function[savedResult,
                      notebook["HaveToSaveAs"] = False;
                      
                      EventFire[GlobalMessanger, "Notebook:Saving:Finished", True];

                      EventFire[promise, Resolve, True];
                      WebUILocation[ StringJoin["/", URLEncode[ notebook["Path"] ]], client, "Target"->"self" ];
                    ], 
                    Function[error,
                      EventFire[GlobalMessanger, "Notebook:Saving:Finished", True];

                      EventFire[GlobalMessanger, "Error", "Failed to save a notebook!"];
                    ]
                  ];                  
              ];
          ]  ];

      ]
    ,
      EventFire[GlobalMessanger, "Notebook:Saving:Started", True];

      Then[saveNotebook[notebook, "Events"->AppEvent, "Modals"->ModalController, "Client"->client], 
        Function[result,
          EventFire[GlobalMessanger, "Notebook:Saving:Finished", True];
          Print[result];
          EventFire[promise, Resolve, True];
        ], 
        Function[error,
          EventFire[GlobalMessanger, "Notebook:Saving:Finished", True];
          EventFire[GlobalMessanger, "Error", "Failed to save a notebook!"];
        ]
      ]
    ]
  ];

  promise
]

dumpNotebookAs[assoc_Association, GlobalMessanger_, ModalController_, client_] := With[{notebook = assoc["Notebook"], path = assoc["Path"], promise = Promise[]},
If[TrueQ[notebook["ReadOnly?"]],
  EventFire[GlobalMessanger, "Error", "This is read-only notebook"];
,
  notebook["HaveToSaveAs"] = False;
  notebook["WorkingDirectory"] = .;
  

  EventFire[GlobalMessanger, "Notebook:Saving:Started", True];

  Then[cloneNotebook[notebook, path, "Modals"->ModalController, "Client"->client], 
    Function[result,
      EventFire[GlobalMessanger, "Notebook:Saving:Finished", True];
      EventFire[promise, Resolve, True];
      WebUILocation[ StringJoin["/", URLEncode[ path ] ], client ];
    ], Function[error,
      EventFire[GlobalMessanger, "Notebook:Saving:Finished", True];
      EventFire[GlobalMessanger, "Error", "Failed to save a notebook!"];
    ]
  ]
  
];
promise
]

UIPrintPDF;

printPDF[client_, ModalController_, log_] := Module[{spinner = Notifications`Spinner["Topic"->"Printing", "Body"->"Please, wait"]}, 
EventFire[log, spinner, True];
Then[WebUIFetch[UIPrintPDF[
  "crop"->False, "printBackground"->True, "pageSize"->"A4", "scale"->1, 
  "generateDocumentOutline" -> True, "HeaderCSS"->"",
  "margins"-><|"right"->0, "left"->0, "top"->0, "bottom"->0|>
], client, "Format"->"JSON"], Function[payload,
  With[{data = ByteArray[payload]},

      Delete[spinner];
      If[Length[data] == 0,
        EventFire[log, "Error", "Printing requires WLJS Desktop App"];
        Return[Null, Module];
      ];

      With[{
          p = Promise[]
      },
          EventFire[ModalController, "SaveDialog", <|
              "Promise"->p,
              "title"->"Save PDF",
              "properties"->{"createDirectory", "dontAddToRecent"},
              "filters"->{<|"extensions"->"pdf", "name"->"PDF Document"|>}
          |>];
          
          Then[p, Function[result, 
              Module[{filename = If[StringQ[result], URLDecode @ result, URLDecode @ result["filePath"]], file},
                  If[!StringQ[filename] || TrueQ[result["canceled"] ] || StringLength[filename] === 0, 
                    Return[];
                  ];

                  If[!StringMatchQ[filename, __~~".pdf"],  filename = StringJoin[filename, ".pdf"]];
                  If[filename === ".pdf", filename = StringJoin[name, filename] ];
                  file = OpenWrite[filename, BinaryFormat->True];
                  BinaryWrite[file, data];
                  Close[file];

                  EventFire[log, "Saved", StringJoin["Exported to ", filename]];
              ] 
          ]]

      ]
  ]
]] ];


ExtensionsJS = (StringJoin["/", FileNameToUURLPath[#]]) &/@ WLJSPackages`Includes["js"];
ExtensionsStyles = With[{Path = StringJoin["/", FileNameToUURLPath[#]]},
  <link rel="stylesheet" href="{Path}"/> 
] &/@ WLJSPackages`Includes["styles"] // ToStringRiffle;

defaultHomeFolder = If[KeyExistsQ[$Env, "folder"], $Env["folder"], Join[<|"Home"->AppExtensions`DefaultDocumentsDir (*`*)|>, settings]["Home"] ];

ExtensionMenuItems = <|"file"->{}, "misc"->{}, "kernel"->{}, "view"->{}, "edit"->{}|>;

Map[Function[item, 
  If[KeyExistsQ[item, "section"], 
    With[{sec = item["section"]},
      ExtensionMenuItems[sec] = Append[ExtensionMenuItems[sec], item]
    ]
  ,
    ExtensionMenuItems["misc"] = Append[ExtensionMenuItems["misc"], item]
  ];
], Select[Flatten[WLJSPackages`Packages[#, "wljs-meta", "menu"] &/@ Keys[WLJSPackages`Packages]], AssociationQ]];

App[request_] := With[{
  Secret = CreateUUID[], 
  GlobalControls  = CreateUUID[],
  ModalController = CreateUUID[],
  GlobalMessanger = CreateUUID[],
  Path = Switch[$OperatingSystem,
    "Windows",  
    URLDecode @ StringDrop[request["Path"],1],  
    
    "Linux",
    URLDecode @ StringDrop[request["Path"],1 ],

    _,
    URLDecode @ StringDrop[request["Path"],1 ]]
},

  Print["## Loading by path : "]; Print[Path];

  
  With[{
        cachedClient = Unique["cachedClient"],
        SidebarId = CreateUUID[],
        OnLoad    = CreateUUID[],

        accentColor = With[{c = Lookup[settings, "AccentColor", "System"]}, If[c === "System", Lookup[System`$Env, "AccentColor", "#008855"], c]],

        kernels = Hold[AppExtensions`KernelList] (*`*),

        isElectron = ElectronQ[request],
        
        GlobalParameters = Join[<|"Navigator"->(If[KeyExistsQ[#, "AppOS"], #["AppOS"], "Browser"] &@ request["Headers"]), "ElectronQ" -> ElectronQ[request], "AccentColor"->"teal", "BackupTimeout" -> (3 60 1000)|>, settings],
        AppClass = If[ElectronQ[request], "", "dark:bg-gray-800"],
        NavigatorOS = If[KeyExistsQ[#, "AppOS"], #["AppOS"], "Browser"] &@ request["Headers"],
        
        FrontEndCustomCSS = If[KeyExistsQ[settings, "FrontEndCustomCSS"], If[
          StringLength[settings["FrontEndCustomCSS"]] > 0,
            With[{CSSValue = settings["FrontEndCustomCSS"]},
              <style>
                <CSSValue/>
              </style>
            ]
          ,
            ""
        ], ""],

        Transparency = If[KeyExistsQ[settings, "Transparency"], If[settings["Transparency"], "true", "false"], "false"],

        FakeMenu = 
          Switch[If[KeyExistsQ[#, "AppOS"], #["AppOS"], "Browser"] &@ request["Headers"],
            "Browser",
              With[{middleware = CreateUUID[]},
                EventHandler[middleware, {
                  "NewNotebook" -> Function[Null,
                    EventFire[GlobalControls, "NewNotebook", True]
                  ],

                  "LocateExamples" -> Function[Null,
                    WebUILocation[StringJoin["/folder/", URLEncode[AppExtensions`DemosDir (*`*)]], $Client, "Target"->_, "Features"->"top=100,right=100"];
                  ],

                  "OpenSettings" -> Function[Null,
                    WebUILocation["/settings", $Client, "Target"->_];
                  ],

                  "Exit" -> Function[Null,
                    Exit[0];
                  ],

                  any_String :> (EventFire[GlobalControls, any, #]&)
                }];
              

                BrowserMenu["Event" -> middleware, "Plugins"->ExtensionMenuItems, "Navigator"->(If[KeyExistsQ[#, "AppOS"], #["AppOS"], "Browser"] &@ request["Headers"])]
              ]
            ,

            "OSX",
              ""
            ,

            _,
              FakeMenu["Plugins" -> ExtensionMenuItems, "Controls"-> GlobalControls,  "Class"->"ml-1", "Style"->""]

          ]
    },

    (* /* collect any runtime changes  */ *)
    loadSettingsOnceMore;

    EventHandler[OnLoad, Function[Null,
      cachedClient = $Client;
      EventFire[AppEvent, "AfterUILoad", <|"Client"->$Client, "Settings"->settings, "Env"->env, "Controls"->GlobalControls|>];
      
      With[{socket = EventClone[$Client]},
        EventHandler[socket, {"Closed" -> Function[Null,
          EventRemove[socket];  
          Echo["App >> Destroy"];
          EventRemove[Secret];
          EventRemove[GlobalControls];
          EventRemove[ModalController];
          EventRemove[GlobalMessanger];
          ClearAll[cachedClient];
          EventFire[AppEvent, "AppUIDestroy", True];
        ]}]
      ];

      If[settings["FirstLaunch"] =!= False,
        settings = Join[settings, <|"FirstLaunch" -> False|>];
        storeSettings[settings];              
      ];
      
      If[env["AppJSON", "version"] =!= settings["CurrentVersion"], With[{version = env["AppJSON", "version"]},
        settings = Join[settings, <|"CurrentVersion" -> version|>];
        storeSettings[settings];
      ] ];

      EventRemove[OnLoad];
    ]];

    (* /* redirect to extensions  */ *)
    EventHandler[EventClone[GlobalMessanger], {
      any_ :> (EventFire[AppEvent, Messanger[any], #]&)
    }];

    (* /* redirect to extensions  */ *)
    EventHandler[EventClone[GlobalControls], {
      any_ :> (EventFire[AppEvent, any, #]&)
    }]; 
   
    (*/* Hard logic */*)
    EventHandler[EventClone[GlobalControls], {
      "OpenSettings" -> (WebUILocation[StringJoin["/settings", ""], $Client, "Target"->_, "Features"->"width=600,height=800"]&), 
      "RenameFile" -> ((renameFile @@ #) &),
       ("NewNotebook" | "_NewNotebook" | "_NewQuickNotebook") -> (createNotebook[<|"Quick"->True, "HaveToSaveAs"->True|>, <|"BaseDirectory"->AppExtensions`QuickNotesDir(*`*)|>, GlobalMessanger, ModalController, $Client, Path]&),
      "DumpSave" -> (dumpNotebook[#, GlobalMessanger, ModalController, $Client]&),
      "DumpSaveAs" -> (dumpNotebookAs[#, GlobalMessanger, ModalController, $Client]&),
      "DumpSaveTemporal" -> (backupNotebook[#, GlobalMessanger, $Client]&),
      "ReopenAsQuick" -> (reopenNotebookAsQuick[#, GlobalMessanger, ModalController, $Client]&),
      "PrintPDF" -> (printPDF[$Client, ModalController, GlobalMessanger]&)
    }];
  {
    "<!DOCTYPE html>"
  ,

    <html class="h-full {AppClass}" os="{NavigatorOS}" transparency="{Transparency}" > 
        <Head Title={FileNameTake[Path]} Settings={settings} AccentColor={accentColor}>
            <meta charset="utf-8"/>
            <WLJSHeader List={ExtensionsJS}/>  
            <WLJSTransportScript TwoKernels={True} Port={$Env["ws"]} PrefixMode={$Env["wsprefix"]}/>     
            <WebUIInitializationScript/>
            <ExtensionsStyles/>
            <FrontEndCustomCSS/>
        </Head>  
        <body class="h-full owin:border owin:border-slate-400 linux:border linux:border-slate-400"> 
        <WebUIOnLoad Event={OnLoad}/>
        <ExtensionTemplates Template={"AppHead"} />
        <div class="h-full">
          <Modals ModalsPort={ModalController} ElectronQ={isElectron} CachedClient={cachedClient}/>
          <ExtensionTemplates Template={"AppOverlay"} Path={Path} AppEvents={AppEvent} MessagePort={GlobalMessanger}/>
          <Kernels AppEvents={AppEvent} Controls={GlobalControls} Messager={GlobalMessanger} KernelList={kernels} OnConnected={initKernel[serverParams]} OnExit={deinitKernel}/>

          <div class="h-full" id="frame">
            <div class="h-full flex flex-col" style="-webkit-app-region: drag">
              <TopBar Menu={FakeMenu} CachedClient={cachedClient} ExtensionTemplates={ExtensionTemplates} AppEvents={AppEvent} Messanger={GlobalMessanger} Parameters={GlobalParameters} Controls={GlobalControls} Path={Path} Modals={ModalController}/>
              <Notifications Controls={GlobalControls} CachedClient={cachedClient} MessagePort={GlobalMessanger} ElectronQ={isElectron}/>
              <Views Parameters={GlobalParameters} ElectronQ={isElectron} Modals={ModalController} Kernels={kernels} Path={Path} AppEvents={AppEvent} Messanger={GlobalMessanger} Controls={GlobalControls} ExtensionTemplateInjection={ExtensionTemplates}/>
              <ExtensionTemplates Template={"AppFooter"} Path={Path} AppEvents={AppEvent} MessagePort={GlobalMessanger}/>
            </div> 
          </div>
        </div>
        <Script Secret={Secret}/>
        <ExtensionTemplates Template={"AppScripts"} />
        <ElectronBinds Controls={GlobalControls}/>
      </body>
    </html>
  } // StringRiffle
] ];

Script[Rule["Secret", Secret_]] := With[{},
  <script type="module">
    core.UIPrintPDF = async (args, env) => {
      if (!(window?.electronAPI?.toPDF)) {
          return [];
      }
      const options = await core._getRules(args, env);  
      const p = new Deferred();
      electronAPI.toPDF(options, (result)=>{
        p.resolve(Array.from(result));
      })
      return p.promise;      
    }
  </script>
];

http["MessageHandler", "App"] = AssocUMatchQ[<|"Method" -> "GET"|>] -> middleware[App];


Print["App Loaded!"];



